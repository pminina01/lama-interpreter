-- Lama.cf

Prog. Program ::= [Stm] ;

terminator Stm "" ;

SDecl.   Stm ::= Type Ident ";"  ;
SAss.    Stm ::= Ident "=" Exp ";" ;
SInit.   Stm ::= Type Ident "=" Exp ";" ;
SBlock.  Stm ::= "{" [Stm] "}" ;
SPrint.  Stm ::= "print" Exp  ";" ;

EVar.    Exp7 ::= Ident ;
EInt.    Exp7 ::= Integer ;
EDouble. Exp7 ::= Double ;
EBool.   Exp7 ::= Bool ;
EPostIncr.  Exp6  ::= Ident "++" ;
EPostDecr.  Exp6  ::= Ident "--" ;
EPreIncr.   Exp5  ::= "++" Ident ;
EPreDecr.   Exp5  ::= "--" Ident ;
EMul.       Exp4  ::= Exp4 "*" Exp5 ;
EDiv.       Exp4  ::= Exp4 "/" Exp5 ;
EAdd.       Exp3  ::= Exp3 "+" Exp4 ;
ESub.       Exp3  ::= Exp3 "-" Exp4 ;
ELess.      Exp1  ::= Exp2 "<"  Exp2 ;
EGreater.   Exp1  ::= Exp2 ">"  Exp2 ;

rules Bool ::= "true" | "false";

coercions Exp 7 ;

TInt.    Type ::= "int" ;
TDouble. Type ::= "double" ;
TBool.   Type ::= "bool" ;
