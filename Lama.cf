-- Lama.cf

Prog. Program ::= [Stm] ;

terminator Stm "" ;

SDecl.   Stm ::= Type Ident ";"  ;
SAss.    Stm ::= Ident "=" Exp ";" ;
SInit.   Stm ::= Type Ident "=" Exp ";" ;
SBlock.  Stm ::= "{" [Stm] "}" ;
SPrint.  Stm ::= "print" Exp  ";" ;

EVar.    Exp5 ::= Ident ;
EInt.    Exp5 ::= Integer ;
EDouble. Exp5 ::= Double ;
EBool.   Exp5 ::= Bool ;
EPostIncr.  Exp4  ::= Ident "++" ;
EPostDecr.  Exp4  ::= Ident "--" ;
EPreIncr.   Exp3  ::= "++" Ident ;
EPreDecr.   Exp3  ::= "--" Ident ;
EMul.       Exp2  ::= Exp2 "*" Exp3 ;
EDiv.       Exp2  ::= Exp2 "/" Exp3 ;
EAdd.       Exp1  ::= Exp1 "+" Exp2 ;
ESub.       Exp1  ::= Exp1 "-" Exp2 ;

rules Bool ::= "true" | "false";

coercions Exp 5 ;

TInt.    Type ::= "int" ;
TDouble. Type ::= "double" ;
TBool.   Type ::= "bool" ;
